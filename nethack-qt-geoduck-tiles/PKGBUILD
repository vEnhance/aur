# Maintainer: Evan Chen <evan@evanchen.cc>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor :	Harry Stern <harry.stern@rutgers.edu>
# Contributor : kevin <kevin@archlinux.org>
# Contributor : Christian Schmidt <mucknert@gmx.net>
# Contributor : Markus Meissner <markus@meissna.de>
# Contributor : Nick Erdmann <erdmann@date.upb.de>

pkgname=nethack-qt-geoduck-tiles
_pkgname=nethack
pkgver=3.6.7
pkgrel=1
pkgdesc="A single player dungeon exploration game. QT version with Geoduck's tiles"
arch=('i686' 'x86_64')
url="http://www.nethack.org/index.html"
license=('custom')
depends=('gzip' 'ncurses' 'qt5-base' 'qt5-multimedia' 'libxaw' 'libxt' 'libx11')
makedepends=('xorg-mkfontscale' 'xorg-bdftopcf')
conflicts=('nethack' 'nethack-qt' 'nethack-x11')
provides=('nethack')
source=("https://nethack.org/download/${pkgver}/${_pkgname}-${pkgver//./}-src.tgz"
  "http://www.geoduckthings.net/nhack/geoduck25x15-2017.bmp"
  "http://www.geoduckthings.net/nhack/geoduck50x30-2017.bmp"
)
md5sums=('eef55cf2d94f637386bf9beefdbe44dc'
  'c98c2cad5636a296e53a2a26594c610b'
  '4afd16f03056480b2d890ff4261e0d94')
_srchome=NetHack-${pkgver}

prepare() {
  #cd "$_pkgname-$pkgver/"
  cd $_srchome

  sed -e 's|^/\* \(#define LINUX\) \*/|\1|' \
    -e 's|^/\* \(#define TIMED_DELAY\) \*/|\1|' \
    -e 's|^/\* #define VAR_PLAYGROUND .*|#define VAR_PLAYGROUND "/var/games/nethack-qt"|' -i include/unixconf.h

  # we are setting up for setgid games, so modify all necessary permissions
  # to allow full access for groups

  sed -e '/^HACKDIR/ s|/games/lib/\$(GAME)dir|/usr/share/nethack-qt/|' \
    -e '/^SHELLDIR/ s|/games|/usr/bin|' \
    -e '/^VARDIRPERM/ s|0755|0775|' \
    -e '/^VARFILEPERM/ s|0600|0664|' \
    -e '/^GAMEPERM/ s|0755|02755|' \
    -e 's|\(DSYSCF_FILE=\)\\"[^"]*\\"|\1\\"/var/games/nethack-qt/sysconf\\"|' \
    -e '/^VARDIR/ s|\$(HACKDIR)|\$(PREFIX)/var/games/nethack-qt|' \
    -e 's|\(DHACKDIR=\)\\"[^"]*\\"|\1\\"/usr/share/nethack-qt/\\"|' -i sys/unix/hints/linux-qt5

  sed -e 's|^#GAMEUID.*|GAMEUID = root|' \
    -e 's|^#GAMEGRP.*|GAMEGRP = games|' \
    -e '/^FILEPERM\s*=/ s|0644|0664|' \
    -e '/^DIRPERM\s*=/ s|0755|0775|' -i sys/unix/Makefile.top

  sed -e "/^MANDIR\s*=/s|/usr/man/man6|$pkgdir/usr/share/man/man6|" -i sys/unix/Makefile.doc
}

build() {
  cd $_srchome/sys/unix
  sh setup.sh hints/linux-qt5

  cd "${srcdir}/$_srchome"
  make GAME=nethack-qt
}

package() {
  cd $_srchome

  install -dm755 "$pkgdir"/usr/share/{man/man6,doc/nethack-qt}
  install -dm775 "$pkgdir"/var/games/
  make PREFIX="$pkgdir" GAME=nethack-qt -j1 install manpages # Multi-threaded builds fail.
  sed -e "s|HACKDIR=$pkgdir/|HACKDIR=/|" \
    -e 's|HACK=$HACKDIR|HACK=/usr/share/nethack-qt|' \
    -i "$pkgdir"/usr/bin/nethack-qt

  mv "$pkgdir"/usr/share/nethack-qt/sysconf "$pkgdir"/var/games/nethack-qt
  install -Dm644 ../geoduck25x15-2017.bmp "$pkgdir"/usr/share/nethack-qt/
  install -Dm644 ../geoduck50x30-2017.bmp "$pkgdir"/usr/share/nethack-qt/

  # FS#43414: /var/games should be owned by root:games.
  chown -R root:games "$pkgdir"/var/games/nethack-qt/
  chown root:games "$pkgdir"/usr/share/nethack-qt/nethack-qt
  chmod 02755 $pkgdir/usr/share/nethack-qt/nethack-qt

  install -Dm644 doc/Guidebook.txt "$pkgdir"/usr/share/doc/nethack-qt/Guidebook.txt
  install -Dm644 dat/license "$pkgdir"/usr/share/licenses/nethack-qt/LICENSE
}
